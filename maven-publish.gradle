apply from: 'secrets.gradle'

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

signing {
    useGpgCmd()
    sign publishing.publications
}

nexusStaging {
    username = loadSecret("SONATYPE_USERNAME")
    password = loadSecret("SONATYPE_PASSWORD")
    delayBetweenRetriesInMillis = 5000
}

publishing {
    repositories {
        maven {
            name = "Sonatype"
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = loadSecret("SONATYPE_USERNAME")
                password = loadSecret("SONATYPE_PASSWORD")
            }
        }
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/gianluz/danger-kotlin-android-lint-plugin")
            credentials {
                username = loadSecret("GITHUB_PACKAGES_USERNAME")
                password = loadSecret("GITHUB_PACKAGES_PASSWORD")
            }
        }
    }

    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourceJar {
                classifier "sources"
                from sourceSets.main.allJava
            }
            artifact javadocJar {
                classifier "javadoc"
                from javadoc.destinationDir
            }
            pom {
                name = 'Danger Kotlin - Android Lint Plugin'
                description = 'A plugin for Danger Kotlin for Android Lint'
                url = "https://github.com/gianluz/danger-kotlin-android-lint-plugin"
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://github.com/gianluz/danger-kotlin-android-lint-plugin/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'gianluz'
                        name = 'Gianluca Zuddas'
                        email = 'gianluca.zuddas@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/gianluz/danger-kotlin-android-lint-plugin.git'
                    developerConnection = 'scm:git:ssh://github.com/gianluz/danger-kotlin-android-lint-plugin.git'
                    url = 'https://github.com/gianluz/danger-kotlin-android-lint-plugin'
                }
            }
        }
    }

    gradle.taskGraph.whenReady { taskGraph ->
        if (taskGraph.allTasks.any { it instanceof Sign }) {
            allprojects {
                ext."signing.gnupg.keyName" = loadSecret("GPG_KEY_ID")
                ext."signing.gnupg.passphrase" = loadSecret("GPG_KEY_PASSWORD_SECRET")
            }
        }
    }
}